plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.bintray'
}

tasks.withType(JavaCompile) {
    options.incremental = true
    options.encoding = 'UTF-8'
}

project.version = '1.0.0'
ext.moduleName = 'opus-java-api'
archivesBaseName = moduleName

dependencies {
    implementation 'net.java.dev.jna:jna:4.4.0'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from 'src/main/java'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

build.dependsOn sourcesJar
build.dependsOn javadocJar

bintrayUpload {
    dependsOn build

    onlyIf { !getProjectProperty("bintrayUsername").empty }
    onlyIf { !getProjectProperty("bintrayApiKey").empty }
}

bintray {
    user = bintrayUsername
    key = bintrayApiKey
    publications = ["BintrayRelease"]
    pkg {
        repo = 'maven'
        name = moduleName
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/discord-java/opus-java.git'
        githubRepo = 'discord-java/opus-java'
        publish = true
        version {
            name = project.version
            vcsTag = project.version
            released = new Date()
            gpg {
                sign = true
            }
        }
    }
}

String getProjectProperty(String propertyName) {
    return hasProperty(propertyName) ? this.properties[propertyName] : ""
}

publishing {
    publications {
        BintrayRelease(MavenPublication) {
            from components.java
            groupId project.group
            artifactId moduleName
            version version

            artifact sourcesJar
            artifact javadocJar
        }
    }
}