plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

tasks.withType(JavaCompile) {
    options.incremental = true
    options.encoding = 'UTF-8'
}

project.version = rootProject.version
ext.moduleName = 'opus-java-api'
archivesBaseName = moduleName

dependencies {
    implementation 'net.java.dev.jna:jna:4.4.0'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from 'src/main/java'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

build.dependsOn sourcesJar
build.dependsOn javadocJar

task signJar(type: Sign, dependsOn: jar) {
    sign(jar)
}

task signSourcesJar(type: Sign, dependsOn: sourcesJar) {
    sign(sourcesJar)
}

task signJavadocJar(type: Sign, dependsOn: javadocJar) {
    sign(javadocJar)
}

publishing {
    publications {
        Release(MavenPublication) {
            from components.java
            groupId project.group
            artifactId moduleName
            version version

            if (canSign) {
                artifact(signJar.signatureFiles[0]) {
                    classifier = null
                    extension = "jar.asc"
                }
                artifact(signSourcesJar.signatureFiles[0]) {
                    classifier = "sources"
                    extension = "jar.asc"
                }
                artifact(signJavadocJar.signatureFiles[0]) {
                    classifier = "javadoc"
                    extension = "jar.asc"
                }

                artifact(signPom.signatureFiles[0]) {
                    classifier = null
                    extension = "pom.asc"
                }
                artifact(signModule.signatureFiles[0]) {
                    classifier = null
                    extension = "module.asc"
                }
            }

            artifact sourcesJar
            artifact javadocJar

            generatePom(pom, "jar")
        }
    }
}

signModule.dependsOn generateMetadataFileForReleasePublication
signPom.dependsOn generatePomFileForReleasePublication

task signFiles {
    dependsOn(signPom, signModule, signSourcesJar, signJavadocJar, signJar)
}
